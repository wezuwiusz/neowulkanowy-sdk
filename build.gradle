plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.71' apply false
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
    id 'com.jfrog.bintray' version '1.8.4'
}

ext {
    PUBLISH_VERSION = '0.17.0'
    SITE_URL = 'https://github.com/wulkanowy/sdk'
    GIT_URL = 'https://github.com/wulkanowy/sdk.git'

    jspoon = "1.3.2"
    okhttp3 = "3.12.10"
    retrofit = "2.6.4"
    threetenbp = "1.4.2"
    slf4j = "1.7.30"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'kotlin'
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: 'com.jfrog.bintray'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    version = PUBLISH_VERSION
    group = "io.github.wulkanowy"

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        configurations = ['archives']

        pkg {
            repo = 'wulkanowy'
            name = 'sdk'
            desc = 'Unified way of retrieving data from the UONET+ register through mobile api and scraping api'

            websiteUrl = 'https://github.com/wulkanowy/sdk'
            issueTrackerUrl = 'https://github.com/wulkanowy/sdk/issues'
            vcsUrl = 'https://github.com/wulkanowy/sdk.git'
            licenses = ['Apache-2.0']
            userOrg = 'wulkanowy'
            labels = ['wulkanowy', 'sdk']
            publicDownloadNumbers = true
            publish = true

            version {
                name = PUBLISH_VERSION
                vcsTag = PUBLISH_VERSION
                released  = new Date()
            }
        }
    }
}

subprojects {
    apply plugin: 'jacoco'

    ktlint {
        additionalEditorconfigFile = file(".editorconfig")
        disabledRules = [
            "no-wildcard-imports",
            "import-ordering",
            "max-line-length"
        ]
    }

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.6"
            javaParameters = true
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        implementation "io.reactivex.rxjava2:rxjava:2.2.19"
        implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3"

        compileOnly "org.threeten:threetenbp:$threetenbp:no-tzdb"
        testImplementation "org.threeten:threetenbp:$threetenbp"

        testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3"
        testImplementation "junit:junit:4.13"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        testLogging.showStandardStreams = false
    }

    group = "io.github.wulkanowy.sdk"
    if (project.plugins.hasPlugin('java')) {

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }
}

dependencies {
    compile project(":sdk")
    compile project(":sdk-mobile")
    compile project(":sdk-scrapper")

    compile "com.squareup.okhttp3:okhttp:$okhttp3"
}
